include "./trees/merkle_check.pil";
include "precomputed.pil";
include "public_inputs.pil";
include "constants_gen.pil";
include "poseidon2_hash.pil";

/**
 * This internal gadget constrains the retrieval of the derived address given the
 * canonical address of a protocol contract.
 * It computes the leaf hash using the preimage {canonical_addr, derived_addr} and
 * performs the membership check against the protocol contract tree.
 * The use of the correct tree is guaranteed by a constrained lookup of the root used
 * in the membership check with the root contained in the public inputs.
 * The root in the public inputs is constrained by upstream circuits (and eventually L1)
 * 
 * N.B: We do not currently support non-membership checks, however this can be updated to
 * support non-membership checks.
 * 
 * Usage (from contract_instance_retrieval.pil):
 * contract_instance_retrieval.is_protocol_contract {
 *   contract_instance_retrieval.address,
 *   contract_instance_retrieval.derived_address
 * } in
 * sel { canonical_address, derived_address };
 *
 */
namespace protocol_contract;

    pol commit sel;
    sel * (1 - sel) = 0;

    #[skippable_if]
    sel = 0;

    pol commit canonical_address;
    pol commit derived_address;

    pol commit pi_index; // todo: while we don't support constants in lookups
    sel * (pi_index - constants.AVM_PUBLIC_INPUTS_PROTOCOL_CONTRACT_TREE_ROOT) = 0;
    pol commit root;
    #[PUBLIC_INPUT_PROTOCOL_CONTRACT_ROOT]
    sel { pi_index, root }
    in
    public_inputs.sel { precomputed.clk, public_inputs.cols[0] };

    pol commit tree_depth; // todo: while we don't support constants in lookups
    sel * (constants.PROTOCOL_CONTRACT_TREE_HEIGHT - tree_depth) = 0;

    pol commit next_derived_address; // Is this ok to be purely hinted?
    pol commit leaf_hash;
    #[LEAF_HASH]
    sel { sel, derived_address, next_derived_address, precomputed.zero, leaf_hash }
    in
    poseidon2_hash.end { poseidon2_hash.start, poseidon2_hash.input_0, poseidon2_hash.input_1, poseidon2_hash.input_2, poseidon2_hash.output };

    #[MERKLE_CHECK]
    sel { leaf_hash, canonical_address, tree_depth, root }
    in 
    merkle_check.start { merkle_check.read_node, merkle_check.index, merkle_check.path_len, merkle_check.read_root };
